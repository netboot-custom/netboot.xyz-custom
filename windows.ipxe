#!ipxe

# Microsoft Windows
# https://www.microsoft.com
# adpated from https://github.com/netbootxyz/netboot.xyz/discussions/757

set winpe_arch x64
set win_image Win10_20H2_English_x64
goto ${menu} ||

:windows
set os Microsoft Windows
clear win_version
menu ${os} 
item --gap Installers
item win_install ${space} Boot Windows 10 x64 Installer
item win_cmd ${space} Boot Windows 10 x64 Command Line
item --gap Options:
#item image_set ${space} Image [ ${win_image} ]
item url_set ${space} Base URL [ ${win_base_url} ]
choose win_version || goto windows_exit
goto ${win_version}

:image_set
menu Image
item Win10_1909_English_x64 Win10_1909_English_x64
item Win10_20H2_English_x64 Win10_20H2_English_x64
choose win_image && goto windows

:pe_arch_set
iseq ${winpe_arch} x64 && set winpe_arch x86 || set winpe_arch x64
goto windows

:url_set
echo Set the HTTP URL of an extracted Windows ISO without the trailing slash:
echo e.g. http://www.mydomain.com/windows
echo
echo -n URL: ${} && read win_base_url
echo
echo netboot.xyz will attempt to load the following files:
echo ${win_base_url}/x64/bootmgr
echo ${win_base_url}/x64/bootmgr.efi
echo ${win_base_url}/x64/boot/bcd
echo ${win_base_url}/x64/boot/boot.sdi
echo ${win_base_url}/x64/sources/boot.wim
echo ${win_base_url}/config/install.bat
echo ${win_base_url}/config/configure.bat
echo ${win_base_url}/config/winpeshl.ini
echo
prompt Press any key to return to Windows Menu...
goto windows

:win_install
isset ${win_base_url} && goto boot_install || echo URL not set... && goto url_set

:win_cmd
isset ${win_base_url} && goto boot_cmd || echo URL not set... && goto url_set

:boot_cmd
imgfree
kernel http://${boot_domain}/wimboot
initrd -n bootmgr     ${win_base_url}/windowspe/bootmgr       bootmgr ||
initrd -n bootmgr.efi ${win_base_url}/windowspe/bootmgr.efi   bootmgr.efi ||      
initrd -n bcd         ${win_base_url}/windowspe/bcd      bcd ||
initrd -n bcd         ${win_base_url}/windowspe/BCD      bcd ||
initrd -n boot.sdi    ${win_base_url}/windowspe/boot.sdi boot.sdi ||
initrd -n boot.wim    ${win_base_url}/windowspe/boot.wim boot.wim
boot

:boot_install
imgfree
kernel http://${boot_domain}/wimboot
initrd ${win_base_url}/config/install.bat install.bat
initrd ${win_base_url}/config/configure.bat configure.bat
initrd ${win_base_url}/config/winpeshl.ini winpeshl.ini
initrd -n bootmgr     ${win_base_url}/windowspe/bootmgr       bootmgr ||
initrd -n bootmgr.efi ${win_base_url}/windowspe/bootmgr.efi   bootmgr.efi ||      
initrd -n bcd         ${win_base_url}/windowspe/bcd      bcd ||
initrd -n bcd         ${win_base_url}/windowspe/BCD      bcd ||
initrd -n boot.sdi    ${win_base_url}/windowspe/boot.sdi boot.sdi ||
initrd -n boot.wim    ${win_base_url}/windowspe/boot.wim boot.wim
boot

:windows_exit
exit 0
